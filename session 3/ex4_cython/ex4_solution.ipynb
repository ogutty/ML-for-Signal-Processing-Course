{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "###### <img src=\"Electronic_Brain.png\" width=\"200\" style=\"float:left\">\n",
    "<h1> Summer 2021 ML Course.</h1>\n",
    "<h2> Exercise 4: Cython version(s) of Ronny Roshbakir's trading algorithm.</h2>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import time\n",
    "import numpy as np\n",
    "\n",
    "%load_ext Cython\n",
    "%load_ext line_profiler\n",
    "\n",
    "# Home-grown scripts & libraries.\n",
    "from ex4_utils import py_naive_profit"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Create a semi-realistic prices array.\n",
    "# Start off with a mostly NaN array with a few 'turning points' (local min/max).\n",
    "prices = np.full(5000, fill_value=np.nan)\n",
    "prices[[0, 1250, 3000, -1]] = [80., 30., 75., 50.]\n",
    "\n",
    "# Linearly interpolate the missing values and add some noise.\n",
    "# NOTICE how the turning (valid) points are selected and all others are interpolated.\n",
    "x = np.arange(len(prices))\n",
    "is_valid = ~np.isnan(prices)                                  # Only look at valid numbers.\n",
    "prices = np.interp(x=x, xp=x[is_valid], fp=prices[is_valid])  # Interpolate between them.\n",
    "prices += np.random.randn(len(prices)) * 2                    # Add normally distributed noise."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%lprun -f py_naive_profit py_naive_profit(prices)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<img src=\"desktop-computer-icon.png\" width=\"90\" style=\"float:left; margin-right: 10px;\">\n",
    "<img src=\"Roni_Roshbakir.png\" width=\"36\" style=\"float:left; margin-right: 10px;\">\n",
    "</br>Please Help Roni perform the brute-force max-profit calculation.</br>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython -a\n",
    "# cython: profile=True\n",
    "import time\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "cimport cython\n",
    "\n",
    "# Re-implement the price generator, this time in Cython (actually it's still Numpy).\n",
    "cdef generate_stock_prices():\n",
    "\n",
    "    # Create a semi-realistic prices array.\n",
    "    # Start off with a mostly NaN array with a few 'turning points' (local min/max).\n",
    "    prices = np.full(5000, fill_value=np.nan)\n",
    "    prices[[0, 1250, 3000, -1]] = [80., 30., 75., 50.]\n",
    "\n",
    "    # Linearly interpolate the missing values and add some noise.\n",
    "    # NOTICE how the turning (valid) points are selected and all others are interpolated.\n",
    "    x = np.arange(len(prices))\n",
    "    is_valid = ~np.isnan(prices)                                  # Only look at valid numbers.\n",
    "    prices = np.interp(x=x, xp=x[is_valid], fp=prices[is_valid])  # Interpolate between them.\n",
    "    prices += np.random.randn(len(prices)) * 2                    # Add normally distributed noise.\n",
    "\n",
    "    return(prices)\n",
    "\n",
    "\"\"\"\n",
    "Implement a Cython version of Ronny's naive algorithm.\n",
    "NOTICE: a numpy \"float\" is actually a C \"double\".\n",
    "WARNING: this is a C function - nothing Pythonic will work inside!\n",
    "WARNING: make sure to type each variable inside, otherwise things will be SLOW.\n",
    "NOTICE: the Numpy array is passed into this C function as is.\n",
    "NOTICE: the \"double[::1]\" declaration means we guarantee the input is a contiguous array,\n",
    "        which (by default) Numpy arrays are (In the 2D case, write \"double[:,::1]\").\n",
    "\"\"\"\n",
    "cdef double cy_naive_profit(double[::1] prices):\n",
    "    cdef double max_profit = -1.0\n",
    "    cdef int i,j\n",
    "    for i in range(len(prices)):\n",
    "        for j in range(i):\n",
    "            if prices[i] - prices[j] > max_profit:\n",
    "                max_profit = prices[i] - prices[j]\n",
    "    return max_profit\n",
    "\n",
    "prices = generate_stock_prices()\n",
    "time1 = time.perf_counter()\n",
    "cy_profit = cy_naive_profit(prices)\n",
    "time2 = time.perf_counter()\n",
    "print(\"Cython naive profit:\", cy_profit, \"time:\", time2 - time1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<img src=\"desktop-computer-icon.png\" width=\"90\" style=\"float:left; margin-right: 10px;\">\n",
    "<img src=\"Roni_Roshbakir.png\" width=\"36\" style=\"float:left; margin-right: 10px;\">\n",
    "</br>DANGER ZONE!!! Help Roni perform the brute-force max-profit calculation with array bounds checking disabled.</br>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython -a\n",
    "import time\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "cimport cython\n",
    "\n",
    "cdef generate_stock_prices():\n",
    "    prices = np.full(5000, fill_value=np.nan)\n",
    "    prices[[0, 1250, 3000, -1]] = [80., 30., 75., 50.]\n",
    "    x = np.arange(len(prices))\n",
    "    is_valid = ~np.isnan(prices)                                  # Only look at valid numbers.\n",
    "    prices = np.interp(x=x, xp=x[is_valid], fp=prices[is_valid])  # Interpolate between them.\n",
    "    prices += np.random.randn(len(prices)) * 2                    # Add normally distributed noise.\n",
    "    return(prices)\n",
    "\n",
    "\"\"\"\n",
    "Implement a version of Ronny's naive algorithm with array bounds checking disabled.\n",
    "SUPER WARNING: disabling array bounds checks is DANGEROUS!\n",
    "               Only do this *AFTER* the code as been *FULLY* DEBUGGED!!!\n",
    "               Don't say I didn't warn you!\n",
    "\"\"\"\n",
    "@cython.boundscheck(False)  # Deactivate bounds checking\n",
    "@cython.wraparound(False)   # Deactivate negative indexing.\n",
    "cdef double cy_naive_profit_no_bounds(double[::1] prices):\n",
    "    cdef double max_profit = -1.0\n",
    "    cdef int i,j\n",
    "    for i in range(len(prices)):\n",
    "        for j in range(i):\n",
    "            if prices[i] - prices[j] > max_profit:\n",
    "                max_profit = prices[i] - prices[j]\n",
    "    return max_profit\n",
    "\n",
    "prices = generate_stock_prices()\n",
    "time1 = time.perf_counter()\n",
    "no_bounds_profit = cy_naive_profit_no_bounds(prices)\n",
    "time2 = time.perf_counter()\n",
    "print(\"No bounds-checking profit:\", no_bounds_profit, \"time:\", time2 - time1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<img src=\"desktop-computer-icon.png\" width=\"90\" style=\"float:left; margin-right: 10px;\">\n",
    "<img src=\"Roni_Roshbakir.png\" width=\"36\" style=\"float:left; margin-right: 10px;\">\n",
    "</br>Please help Roni perform brute-force max-profit calculation in parallel.</br>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython -a\n",
    "import time\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "cimport cython\n",
    "from cython.parallel import parallel, prange\n",
    "\n",
    "cdef generate_stock_prices():\n",
    "    prices = np.full(5000, fill_value=np.nan)\n",
    "    prices[[0, 1250, 3000, -1]] = [80., 30., 75., 50.]\n",
    "    x = np.arange(len(prices))\n",
    "    is_valid = ~np.isnan(prices)                                  # Only look at valid numbers.\n",
    "    prices = np.interp(x=x, xp=x[is_valid], fp=prices[is_valid])  # Interpolate between them.\n",
    "    prices += np.random.randn(len(prices)) * 2                    # Add normally distributed noise.\n",
    "    return(prices)\n",
    "\n",
    "\"\"\"\n",
    "Implement *only* the (naive algorithm's) INNER loop.\n",
    "NOTE: the nogil keyword does NOT release Python's infamous GIL, it only\n",
    "instructs Cython that the function *may* be called without the GIL.\n",
    "\"\"\"\n",
    "@cython.boundscheck(False)  # Deactivate bounds checking\n",
    "@cython.wraparound(False)   # Deactivate negative indexing.\n",
    "cdef double inner_loop_max_profit(int i, double[::1] prices) nogil:\n",
    "    cdef double max_profit = -1.0\n",
    "    cdef double final_price = prices[i]\n",
    "    cdef int j\n",
    "    for j in range(i):\n",
    "        if final_price - prices[j] > max_profit:\n",
    "            max_profit = final_price - prices[j]\n",
    "    return(max_profit)\n",
    "\n",
    "\"\"\"\n",
    "\"\"\"\n",
    "@cython.boundscheck(False)  # Deactivate bounds checking\n",
    "@cython.wraparound(False)   # Deactivate negative indexing.\n",
    "cdef cy_naive_profit_parallel(numpy_prices):\n",
    "    # Make sure to declare types for each and every variable.\n",
    "    cdef int i\n",
    "    cdef int len_prices = len(numpy_prices)\n",
    "    cdef int num_cpus = 8\n",
    "    cdef double global_max_profit = 0.0\n",
    "\n",
    "    # Set up memoryviews to hold the maximal profits possible up to each price index.\n",
    "    # NOTE: the \"double[::1]\" means we're declaring a contiguous array.\n",
    "    cdef double[::1] max_profits = np.zeros(len(numpy_prices))\n",
    "    cdef double[::1] memview_prices = numpy_prices\n",
    "\n",
    "    # In order to run loops in parallel, we instruct Python to disable the GIL (Global Interpreter Lock).\n",
    "    # We also provide the number of threads we want to run in parallel.\n",
    "    # In this case, we define one thread per core (note that some CPUs can run two threads per core).\n",
    "    with nogil, parallel(num_threads=num_cpus):\n",
    "\n",
    "        # NOTE: inside this loop, nothing Pythonic will work (as the GIL is disabled).\n",
    "        # NOTE: we use prange() to implement the OUTER loop in parallel.\n",
    "        # for i in prange(len_prices, schedule='guided'):\n",
    "        for i in prange(len_prices):\n",
    "            max_profits[i] = inner_loop_max_profit(i, memview_prices)\n",
    "\n",
    "    # With max_profits populated, simply iterate to find the max.\n",
    "    for i in range(len(max_profits)):\n",
    "        if max_profits[i] > global_max_profit:\n",
    "            global_max_profit = max_profits[i]\n",
    "\n",
    "    return(global_max_profit)\n",
    "\n",
    "prices = generate_stock_prices()\n",
    "time1 = time.perf_counter()\n",
    "super_ronny_profit = cy_naive_profit_parallel(prices)\n",
    "time2 = time.perf_counter()\n",
    "print(\"Cython naive profit:\", super_ronny_profit, \"time:\", time2 - time1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<img src=\"desktop-computer-icon.png\" width=\"90\" style=\"float:left; margin-right: 10px;\">\n",
    "<img src=\"Batya_Bingo.png\" width=\"80\" style=\"float:left; margin-right: 10px;\">\n",
    "</br>Please construct an example where the parallel computation is faster than the serial.</br>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
